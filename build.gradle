buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.franzbecker:gradle-lombok:3.1.0"
    }
}

plugins {
    id 'java'
    id 'war'
    id 'idea'

    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

apply plugin: 'io.franzbecker.gradle-lombok'

group 'com.atsumeru.web'
version '1.0.3'

compileJava.options.encoding = 'UTF-8'
System.setProperty('file.encoding', 'UTF-8')

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    jcenter()
}

configurations {
    all*.exclude module : 'spring-boot-starter-json'

    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

bootJar {
    manifest {
        attributes(
                'Implementation-Title':  'Atsumeru',
                'Implementation-Version': version)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.50"

    // AnnotationsProcessors
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-web'
    implementation 'org.springframework.security:spring-security-test'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // Tomcat/Servlet
    implementation 'javax.servlet:jstl'
    implementation "org.apache.tomcat.embed:tomcat-embed-jasper"

    // Logs
    implementation "ch.qos.logback:logback-classic:1.2.10"

    // CLI ProgressBar
    implementation 'me.tongfei:progressbar:0.9.3'

    // Image manipulation
    implementation 'net.coobird:thumbnailator:0.4.14'
    implementation 'com.github.trickl:palette:0.1.1'

    // ImageIO
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.9.3'
    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.9.3'

    // Apache Commons
    implementation "commons-io:commons-io:2.5"
    implementation "org.apache.commons:commons-compress:1.20"

    // Comparators
    implementation "net.grey-panther:natural-comparator:1.1"

    // Content detection and Various Archives Support
    implementation "org.apache.tika:tika-core:1.24.1"
    implementation "net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01"
    implementation "net.sf.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01"

    // Caching
    implementation "com.github.ben-manes.caffeine:caffeine:2.9.0"

    // JSOUP
    implementation "org.jsoup:jsoup:1.13.1"

    // JSON
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.json:json:20160212"
    implementation "org.apache.clerezza.ext:org.json.simple:0.4"

    // ORM
    implementation "com.j256.ormlite:ormlite-core:5.1"
    implementation "com.j256.ormlite:ormlite-jdbc:5.1"
    implementation "org.xerial:sqlite-jdbc:3.30.1"

    // FB2 Parser
    implementation files("libs/fb2parser.jar")

    // DjVu Reader
    implementation files("libs/DjVu2Image-1.0.jar")

    // PDFBox
    implementation 'org.apache.pdfbox:pdfbox:2.0.25'

    // Bonjour Service
    implementation "de.mannodermaus.rxjava2:rxbonjour:2.0.0-RC1"
    implementation files("libs/rxbonjour-platform-desktop-2.0.0-RC1.jar")
    implementation('de.mannodermaus.rxjava2:rxbonjour-driver-jmdns:2.0.0-RC1') {
        exclude group: 'org.slf4j'
    }

    // Hash
    implementation 'org.hashids:hashids:1.0.3'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.logging.log4j') {
            details.useVersion '2.17.2'
        }
    }
}